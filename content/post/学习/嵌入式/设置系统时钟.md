---
title: 设置系统时钟
toc: true
date: 2014-07-26 22:03:48
tags:
- tiny6410
categories:
- 学习
- 嵌入式
- 裸机程序
---

系统时钟控制逻辑，在S3C6410中生成所需的系统时钟信号，用于CPU的ARMCLK，AXI/AHB总线外设的HCLK和APB总线外设的PCLK。在S3C6410中有三个PLL，一个仅用于ARMCLK，一个用于HCLK和PCLK，最后一个用于外设，特别用于音频相关的时钟。

<!-- more -->

``` asm
// 相关的设置地址
// GPK端口寄存器
	.equ GPKCON0, 0x7F008800
	.equ GPKCON1, 0x7E008804
	.equ GPKDAT, 0x7F008808
	.equ GPKPUD, 0x7E00880C
// 看门狗寄存器
	.equ WTCON, 0x7E004000



// 往某一地址写入数据
.macro SetReg addr,data
	ldr r0, =\addr
	ldr r1, =\data
	str r1, [r0]
.endm

.text

.global _start
	.arm  // 使用arm模式

_start:

// 设置外设地址0x7000000是起始地址，0x13表示大小为256M
/* The purpose of the Peripheral Port Memory Remap Register is to remap the memory
attributes to Non-Shared Device.
*/
    ldr r0, =0x70000000
    orr r0, r0, #0x13
    mcr p15,0,r0,c15,c2,4       @ 256M(0x70000000-0x7fffffff)

// 关闭看门狗
	SetReg WTCON, 0x0

// 设置系统时钟
// 时钟设置相关寄存器
	.equ APLL_LOCK, 0x7E00F000
	.equ MPLL_LOCK, 0x7E00F004
	.equ EPLL_LOCK, 0x7E00F008
	.equ APLL_CON, 0x7E00F00C
	.equ MPLL_CON, 0x7E00F010
	.equ CLK_SRC, 0x7E00F01C
	.equ CLK_DIV0, 0x7E00F020
	.equ CLK_OUT, 0x7E00F02C
	.equ OTHERS, 0x7E00F900

// 设置时钟锁定时间Required period to generate a stable clock output
// 复位时默认就是0xFFFF,可不设置
	SetReg APLL_LOCK, 0x0000FFFF
	SetReg MPLL_LOCK, 0x0000FFFF
	SetReg EPLL_LOCK, 0x0000FFFF

// 设置为异步模式 当arm时钟和HCLK不一样的时候，要设置成异步
// 系统复位时默认就是异步模式，并使用MPLL的输出作为MCLK这一路的时钟，可以跳过该步骤
	ldr r0, =OTHERS
	ldr r1, [r0]
	bic r1, r1, #0xC0  // 将6、7位清零
	str r1, [r0]

loop_others:				/* 等待，直到CPU进入异步模式 */
	ldr r0, =OTHERS
	ldr r1, [r0]
	and r1, #0xf00      // 读取11：8位，如果是0则表
	cmp r1, #0
	bne loop_others

// 设置时钟的分频系数   后面的配置会使 DOUTAPLL＝ 532MHz,HCLKX2IN=532MHz,MOUTMPLL=532MHz
#define ARM_RATIO    0   /* ARMCLK = DOUTAPLL / (ARM_RATIO + 1) = 532MHz */
#define HCLKX2_RATIO 1   /* HCLKX2 = HCLKX2IN / (HCLKX2_RATIO + 1) = 266MHz */
#define HCLK_RATIO   1   /* HCLK = HCLKX2 / (HCLK_RATIO + 1) = 133MHz */
#define PCLK_RATIO   3   /* PCLK   = HCLKX2 / (PCLK_RATIO + 1) = 66.5MHz */
#define MPLL_RATIO   0   /* DOUTMPLL = MOUTMPLL / (MPLL_RATIO + 1) = 532MHz */
#define CLK_DIV0_VAL (ARM_RATIO) \
					 | (MPLL_RATIO << 4) \
					 | (HCLK_RATIO << 8) \
					 | (HCLKX2_RATIO << 9) \
					 | (PCLK_RATIO << 12)  /* 具体写入CLK_DIV0的值 */
	SetReg CLK_DIV0,CLK_DIV0_VAL

/*
设置系统APLL的倍频
FOUTAPL = MDIV * Fin / (PDIV * 2^SDIV) = 266 * 12 / (3*2^1) = 532MHz
按照上述公式，MDIV，PDIV，SDIV可以有不同的组合计算出同一个结果，但并不建议用其它的组合值，手册原文如下
Although there is the equation for choosing PLL value, we recommend only the values in the PLL value
recommendation table. If you have to use another value, please contact us.
Fin为外部晶振提供的时钟频率，此处为12HMz  FIN : 10MHz ≤ FIN ≤ 20MHz
*/
#define MDIV 266 // APLL_CON[25:16] MDIV: 64 ≤ MDIV ≤ 1023
#define PDIV 3 //  APLL_CON[13:8] PDIV: 1 ≤ PDIV ≤ 63
#define SDIV 1 //  APLL_CON[2:0] SDIV: 0 ≤ SDIV ≤ 5

//APLL_CON的最高位为使能位
#define APLL_CON_VAL (1 << 31) \
					 | (MDIV << 16) \
					 | (PDIV << 8) \
					 | (SDIV)
	SetReg APLL_CON, APLL_CON_VAL


//设置系统MPLL的倍频, 与APLL的倍频相同
	SetReg MPLL_CON, APLL_CON_VAL

// 选择PLL的输出作为时钟源
	SetReg CLK_SRC, 0x03
```
